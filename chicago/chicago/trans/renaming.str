module renaming

imports 
	signatures/-
	analysis
	pp
	libspoofax/stratego/debug
	libspoofax/ui/dialogs
	libspoofax/sdf/pp
	
	libspoofax/term/annotation
  	libspoofax/term/origin
  	libspoofax/analysis/constraint
  
  	statix/runtime/-
  	statixruntime
  	statix/api

  	
rules	    
  rename-action(construct-textual-change, analyze):
    (selected-term, _, ast, _, _) -> (paths, results)
    where
      {| ErrorMessage:
        new-name := <read-new-name>
        ; renamed-ast := <rename(analyze|selected-term, new-name)> ast 
        ; (_, _, result) := <construct-textual-change> (ast, renamed-ast)
        ; results := ""
        ; paths := ""
      <+ show-errors |}    

rename-action-debug :
    (selected-term, _, ast, path, project-path) -> (paths, results)
    with
		(paths, asts) := <get-analysed-asts>
		; analysis := <get-project-analysis>
		; resolution-relation :=  <calc-resolution-relation-project(|analysis)> asts
		; <ppdebug(|"resolution-relation: ")> resolution-relation
		; selected-occ := <find-name-index(|ast, resolution-relation)> selected-term
		; target-indices := <calc-name-cluster> (selected-occ, resolution-relation)
		; new-name := <read-new-name> path
		; renamed-asts := <map(rename-ast(|target-indices, new-name))> asts
		; check-capture(editor-analyze|paths, renamed-asts, resolution-relation)
		; results := <map(pp-chicago-string)> renamed-asts
	
    rename(analyze|selected-term, new-name): ast -> renamed-asts
    where
         analysis := <get-project-analysis>
        ; (paths, asts) := <get-analysed-asts> 
    	; resolution-relation :=  <calc-resolution-relation-project(|analysis)> ast
    	; <ppdebug(|"resolution-relation:")> resolution-relation
    	; selected-occ := <find-name-index(|ast, resolution-relation)> selected-term
	    ; target-indices := <calc-name-cluster> (selected-occ, resolution-relation)
		; renamed-asts := <map(rename-ast(|target-indices, new-name))> asts
		; check-capture(analyze|paths, renamed-asts, resolution-relation)     

rules // Step 1: Calculate Resolution Relation

  calc-resolution-relation-project(|analysis): asts -> res-rels
    where
      res-rels := <mapconcat(calc-resolution-relation-file(|analysis))> asts

  calc-resolution-relation-file(|analysis): ast -> relation
    where
  	  relation := <collect(get-dec-ref-pair(|analysis) <+ get-dec-reflexive-pair(|analysis))> ast
  	
  get-dec-ref-pair(|analysis): t -> (ref-index, dec-index)
    where
  	  <is-string> t
  	  ; dec := <stx--get-ast-property(|analysis, Ref())> t
  	  ; ref-index := <stx--get-ast-index> t
  	  ; dec-index := <stx--get-ast-index> dec

  get-dec-reflexive-pair(|analysis): t -> (dec-index, dec-index)
    where
  	  <is-string> t
  	  ; dec := <stx--get-ast-property(|analysis, Prop("decl"))> 
  	  ; dec-index := <stx--get-ast-index> dec

  is-user-defined: (ref, dec@TermIndex(path, num-index)) -> <id>
    where
      <not(eq)> (num-index, 0)
  	    
rules // Step 2: Find name index in selection
  find-name-index(|ast, res-rel): term -> occ-index
    where
      <check-selection(|term, ast)> ast
      ; occ-index := <collect-one(get-name-index(|res-rel))> term  
        <+ add-error(|"Renaming impossible", "The selected term cannot be renamed.")

  get-name-index(|res-rel): term -> occurrence
    where
      if (<is-list> term) then
        <map(get-name-index(|res-rel))> term
      else
        <is-string> term
        ; term-index := <stx--get-ast-index> term
        ; occurrence := <fetch-elem(res-pair-contains(|term-index))> res-rel
      end	
			
  res-pair-contains(|term-index): (ref-index, dec-index) -> term-index 
    where
      <eq> (term-index, ref-index) <+ <eq> (term-index, dec-index)   


rules //Step 4: Rename terms	 	
  rename-ast(|target-indices, new-name): ast -> renamed-ast
    where
      renamed-ast := <bottomup(try(rename-term(|target-indices, new-name)))> ast

  rename-term(|target-indices, new-name): term -> new-name
    where
      <is-string> term
      ; term-index := <stx--get-ast-index> term
      ; <elem> (term-index, target-indices)
	  
rules //Step 4: Checking for capture
  check-capture(analyze|paths, asts, resolution-relation) =
    (new-asts, new-analysis) := <rerun-analysis(analyze)> (paths, asts)
    ; new-resolution-relation := <calc-resolution-relation-project(|new-analysis); qsort(res-pair-gt)> new-asts
    ; <ppdebug(|"new-resolution-relation: ")> new-resolution-relation
    ; old-resolution-relation := <qsort(res-pair-gt)> resolution-relation
    ; <ppdebug(|"old-resolution-relation: ")> old-resolution-relation
    ; <eq>(old-resolution-relation, new-resolution-relation) 
      <+ add-error(|"Capture detected", "This renaming leads to name capture") 

  rerun-analysis(analyze): (paths, asts) -> (new-asts, new-analysis)
    where
      input := <make-analysis-input> (paths, asts)
      ; analysis-result := <analyze> input
      ; new-asts := <get-new-asts> analysis-result
      ; new-analysis := <get-new-analysis> analysis-result
      
  make-analysis-input: (paths, asts) ->  AnalyzeMulti(project-changes, file-changes, (), ())
    where
      project-analyses := <get-project-constraint-analyses>
      ; analysis := <get-project-entry> project-analyses
      ; project-changes := (".", Cached(analysis))
      ; files := <zip> (paths, asts)
      ; file-changes := <map(make-file-change(|project-analyses))> files
       
  make-file-change(|project-analyses): (path, ast) -> (path, Changed(ast, analysis))
    where
      (_, analysis) := <fetch-elem(get-entry(|path))> project-analyses            
      
  get-new-analysis:  AnalysisResult(analysis-list) -> analysis-object
    where
      (Update(project-analysis, _, _, _)) := <get-project-entry> analysis-list
      ; analysis-object := <get-analysis-object> project-analysis 
		
  get-new-asts:  AnalysisResult(analysis-list) -> analyzed-asts
    where
		analyses := <filter(not(is-project-entry))> analysis-list
		; analyzed-asts := <map(get-analyzed-ast)> analyses
		
  get-analyzed-ast : (_, Full(ast, _, _, _,_)) -> ast		  		
      
  res-pair-gt: ((ref-1, dec-1), (ref-2, dec-2)) -> <id>
    where
      <term-index-gt> (ref-1, ref-2) 
															
  term-index-gt: (TermIndex(path-1, num-index-1), TermIndex(path-2, num-index-2)) -> <id>
    where 
      <string-gt> (path-1, path-2) <+ <gt> (num-index-1, num-index-2)
    
rules // Utility strategies
  read-new-name: _ -> new-name
    where 	
      new-name := <show-input-dialog(|"Rename", "")> "Enter a new name"
      
  get-project-analysis: _ -> analysis-object
    where 	
      project-analyses := <get-project-constraint-analyses>
      ; analysis := <get-project-entry> project-analyses
      ; analysis-object := <get-analysis-object> analysis 	
      
  get-project-entry: entries -> project-analysis
    where
      (_, project-analysis) := <fetch-elem(is-project-entry)> entries
  	
  is-project-entry = get-entry(|".")

  get-entry(|search-path): (path, _) -> <id>
    where
      <eq> (path, search-path)    
    
  get-analysis-object: ProjectAnalysis(_, _, _, analysis-object) -> analysis-object
      
  get-analysed-asts: _ -> (paths, asts)
    where
      project-asts := <get-project-analyzed-asts>
      ; (paths, asts) := <filter(not(is-project-entry));unzip> project-asts	       

  check-selection(|selected-term, ast) =
    <not(eq)> (selected-term, ast)
  	<+ add-error(|"Invalid selection", "Please select a name")
  	
  add-error(|title, message) =
    rules(
      ErrorMessage :+ () -> (title, message)
    ); fail
      
  show-errors =    
    [(title, message) | _] := <bagof-ErrorMessage> ()
    ; <show-dialog(|title, "ERROR")> message
     
  add-error-on-fail(s|title, message) =
    s <+ add-error(|title, message); fail
						  
rules // external rules
	external calc-name-cluster(|)
	
    
